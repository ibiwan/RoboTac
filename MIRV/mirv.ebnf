program      -> { definition } .

definition   -> "scope" ( funcdef | procdef | vardef ) .

funcdef      -> "func" "typename" "identifier" "(" [ formalparams ] ")"  "=" "{" { statement } "}" .

procdef      -> "proc" "identifier" "(" [ formalparams ] ")"  "=" "{" { statement } "}" .

vardef       -> "typename" "identifier" "=" expression ";". 

statement    -> definition 
			  | while
              | cond
              | expression ";" 
              | call ";" 
              | "set" "identifier" "=" expression ";" 
              | "rpn" { rpnelement } ";" 
              | "return" expression  ";" .

expression   -> term { "infixop" expression } .

term         -> "literal" 
              | prebinding
              | indexable [ "[" expression "]" ] .

indexable    -> "identifier" 
              | "prefixop" expression 
              | "apply" ( "identifier" | "fnkw" ) "(" [ actualparams ] ")" 
              | "(" expression ")" .

formalparams -> "typename" "identifier" [ "," formalparams ] .

actualparams -> [ "@" "identifier" "=" ] expression [ "," actualparams ] .

prebinding   -> "prebind" "identifier" "(" actualparams ")" .

cond        -> "cond" "{" { "(" expression ")" "{" { statement } "}" } "}" .

while_s     -> "while" "(" expression ")" "{" { statement } "}" .

rpnelement  -> "identifier" | "infixop" | "prefixop" | "fnkw" | "opkw" | "literal" . 

call        -> ( "tcall" | "call" ) ( "identifier" | "prockw" ) "(" [ actualparams ] ")" .
