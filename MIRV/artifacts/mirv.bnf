program      -> definitions .

definitions  -> definition definitions .
definitions  -> "epsilon" .

definition   -> scope deftype .

deftype      -> funcdef .
deftype      -> procdef .
deftype      -> vardef .

funcdef      -> "func" typename identifier "(" formalparams ")"  "=" block .

procdef      -> "proc" identifier "(" formalparams ")"  "=" block .

vardef       -> typename identifier "=" expression ";". 

block        -> "{" statements "}" . 

statements   -> statement statements .
statements   -> "epsilon" .

statement    -> definition .
statement    -> while .
statement    -> cond .
statement    -> expression ";" .
statement    -> call .
statement    -> "set" identifier "=" expression ";" .
statement    -> "rpn" rpnelements ";" . 
statement    -> "return" expression ";" . 

expression   -> term opterm .

opterm       -> infixop expression opterm .
opterm       -> "epsilon" .

term         -> literal .
term         -> prebinding .
term         -> indexable index .

indexable    -> identifier .
indexable    -> prefixop expression .
indexable    -> application .
indexable    -> "(" expression ")" .

index        -> "[" expression "]" .
index        -> "epsilon" .

application  -> "apply" abinding .

call         -> "call" cbinding ";" .
call         -> "tcall" cbinding ";" .

formalparams -> typename identifier moreformals .
formalparams -> "epsilon" .

moreformals  -> "," formalparams .
moreformals  -> "epsilon" .

actuals      -> "(" actualparams ")" . 

actualparams -> "@" identifier "=" expression moreactuals .
actualparams -> expression moreactuals .
actualparams -> "epsilon" .

moreactuals  -> "," actualparams .
moreactuals  -> "epsilon" .

prebinding   -> "prebind" binding .

binding      -> identifier actuals . 

cbinding     -> binding .
cbinding     -> prockw actuals .

abinding     -> binding .
abinding     -> fnkw actuals .

while        -> "while" "(" expression ")" block .

cond         -> "cond" "{" condelements "}" .

condelements -> "(" expression ")" block condelements .
condelements -> "epsilon" .

rpnelements  -> rpnelement rpnelements .
rpnelements  -> "epsilon" .

rpnelement   -> identifier .
rpnelement   -> infixop .
rpnelement   -> prefixop .
rpnelement   -> fnkw .
rpnelement   -> opkw .
rpnelement   -> literal .

typename     -> "typename" .

fnkw         -> "fnkw" .

scope        -> "scope" .

prockw       -> "prockw" .

prefixop     -> "prefixop" .

infixop      -> "infixop" .

identifier   -> "identifier" .

literal      -> "literal" .

opkw         -> "opkw" .